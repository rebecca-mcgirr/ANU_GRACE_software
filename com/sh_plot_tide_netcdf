#!/bin/csh -f

# script to plot the ocean tide, either as a map of a particular region or as a time series.
# This script replaces sh_plot_tidetoasc. It interacts with the new netcdf format of the tide grids.
#
# P. Tregoning
# 15 September 2016

if ($#argv == 0 ) then
  echo "sh_plot_tide_netcdf -f tide.netcdf -o ocean_mascons_stage4_AGU -F M/T/G -r -Rminlon/maxlon/minlat/maxlat [-c cmin cmax] [-r_ts t_start t_end] [-coords lat lon] [-plot Y/N] [-kb_file plt_kb_file.kb"
  exit
endif

############## defaults #####################
set version = ~/gt
set netcdffile = ""
set epoch = 1
set coords = (0 0)
set plot = "Y"
set flag = "M"
set convert = "N"
set overlay = "Y"
set range = "-Rg"
set proj = "-JR30/17"
set kb_file = ""
set ymin_set = 0
set ymax_set = 0
set yanot_set = " "
set xmin = 0
set xmax = 24
set star = "N"

# min/max default tidal colour range
set cmin = -5
set cmax = 5

set ocean_mascon_file = ocean_mascons

gmtset HEADER_FONT_SIZE 15

######## decode command line ###
while ($#argv > 0 )
  set input = ( $argv )
  switch($input[1])
    case -f:
      set netcdffile  = $input[2] ; shift argv
    breaksw
    case -o:
      set ocean_mascon_file  = $input[2] ; shift argv
    breaksw
    case -F:
      set flag  = $input[2] ; shift argv
    breaksw
    case -e*:
      set epoch = $input[2] ; shift argv
    breaksw
    case -r:
      set range = $input[2] ; shift argv
    breaksw
    case -r_ts:
      set minx = $input[2] 
      set maxx = $input[3] 
    breaksw
    case -c:
      set cmin = $input[2] 
      set cmax = $input[3] 
    breaksw
    case -coords:
      set lat = $input[2] 
      set lon = $input[3] 
    breaksw
    case -plot:
      set plot = $input[2] ; shift argv
    breaksw
    case -kb_file:
      set kb_file = $input[2] ; shift argv
    breaksw
    case -proj:
      set proj  = $input[2] ; shift argv
    breaksw
 endsw
  if ( $#argv > 0 ) shift argv
end
alldone:
######################################################################

####################################
##                                ##
##  Spatial map at a single epoch ##
##                                ##
####################################
if ($flag == "M")then
  echo "Generate a map of tide heights for epoch" $epoch
  set outfile = tide_netcdf_map.ps
# convert the netcdf tide heights into a file of lat/lon/hgt
  echo Running tidecdftoasc $netcdffile $ocean_mascon_file $flag $epoch
  ${version}/util/tidecdftoasc $netcdffile $ocean_mascon_file $flag $epoch | grep ocean_ternary | cut -c1-51 > ocean.dat 

# now plot it
  makecpt -Cpolar -T$cmin/$cmax/0.01 > tide.cpt
  echo psxy ocean.dat -: $proj $range -X2 -Y3 -P -K -Ctide.cpt -Sc0.05 -Ba10000nesw
  psxy ocean.dat -: $proj $range -X2 -Y10 -P -K -Ctide.cpt -Sc0.05 -Ba10000:."$netcdffile Epoch $epoch":nesw > $outfile
  pscoast $proj -R -W1/0/0/0  -A0 -O -K >> $outfile
  psscale -D9/-3/15/0.5h -Ctide.cpt -Ba0.5f0.25:"Tide height (m)":neSW -O >> $outfile

  if($plot == "Y")gs $outfile
  exit
endif

####################################
##                                ##
##  Time series at a single site  ##
##                                ##
####################################
if ($flag == "T")then
  echo "Generate a time series of tide heights at location" $lat $lon
  set outfile = tide_netcdf_ts.ps
# extract the netcdf tide heights for each epoch at this location
  echo "Running tidecdftoasc $netcdffile $ocean_mascon_file $flag $lat $lon "
  echo "this takes about 1 minute ....." `date`
  tidecdftoasc $netcdffile $ocean_mascon_file $flag $lat $lon | grep ocean_ternary | awk '{print ($4 - 1) * 10./60. , $3}' > ocean.dat 

# now plot it
  set yrange = `minmax ocean.dat | awk '{print $6}' | sed s/"<"/" "/ | sed s/">"/" "/ | sed s/"\/"/" "/ `
  psxy ocean.dat -W1/255/0/0 -JX15/10 -R$xmin/$xmax/$yrange[1]/$yrange[2] -Ba12f6:"Hours of day":/0.4f0.2:"Tide height (m)"::."$lat $lon":neSW -X3 -Y10 -K -P > $outfile

  if($plot == "Y")gs $outfile
  exit
endif

####################################
##                                ##
##  Tide beneath a ground track   ##
##                                ##
####################################
if ($flag == "G")then
  echo "Generate a time series of tide heights beneath the GRACE satellites from file " $kb_file
  set outfile = tide_netcdf_grdtrack.ps
  if($kb_file == "")then
    echo You must use the "'-kb_file blah.kb'" command line option to enter a groundtrack file
    exit
  endif

endif










